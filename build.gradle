apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jacoco'

group = 'uk.org.okapibarcode'
archivesBaseName = 'okapibarcode'
version = '0.1.4-HL'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.beust:jcommander:1.48'
    testCompile 'junit:junit:4.12'
    testCompile 'com.google.zxing:javase:3.3.0'
    testCompile 'org.reflections:reflections:0.9.10'
}

sourceCompatibility = 7
targetCompatibility = 7

// When using JDK8+ as the primary JDK, JDK7 should be installed as a secondary JDK to compile
// against (to make sure that we're not inadvertently using newer APIs), and the JDK7_HOME
// environment variable should be set to the home directory of this secondary JDK
// https://blogs.oracle.com/darcy/entry/bootclasspath_older_source
tasks.withType(JavaCompile) {
    doFirst {
        if (System.env.JDK7_HOME != null) {
            options.fork = true
            options.bootClasspath = "$System.env.JDK7_HOME/jre/lib/rt.jar"
        }
    }
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'

jar {
    manifest.attributes(
        'Main-Class': 'uk.org.okapibarcode.OkapiBarcode',
        'Implementation-Title': 'Okapi Barcode',
        'Implementation-Version': version
    )
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// herrlock: removed publishing to maven-repo
